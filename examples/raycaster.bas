10 SCREENWIDTH=319; SCREENHEIGHT=179; MAPWIDTH=24; MAPHEIGHT=24; MOVESPEED=0.5; ROTSPEED=0.3
20 POSX=22.0; POSY=12.0
30 DIRX=-1.0; DIRY=0.0
40 PLANEX=0.0; PLANEY=0.66
50 TIME=0.0; OLDTIME=0.0
60 DIM WORLDMAP(23,23)
70 FOR MY=0 TO 23
80 FOR MX=0 TO 23
90 READ WORLDMAP(MY,MX)
100 NEXT
110 NEXT 
120 WHILE 1; CLS
130 FOR X=0 TO SCREENWIDTH STEP 2
140 CAMERAX = 2.0 * X / SCREENWIDTH - 1.0
150 RAYDIRX = DIRX + PLANEX * CAMERAX
160 RAYDIRY = DIRY + PLANEY * CAMERAX
170 MAPX=CINT(POSX)
180 MAPY=CINT(POSY)
190 DELTADISTX=ABS(1.0/RAYDIRX)
200 DELTADISTY=ABS(1.0/RAYDIRY)
210 PERPWALLDIST = 1.0
220 STEPX=0
230 STEPY=0
240 HIT=0
250 SIDE=0
260 IF RAYDIRX<0.0 THEN STEPX=-1; SIDEDISTX = (POSX - MAPX) * DELTADISTX ELSE STEPX = 1; SIDEDISTX = (MAPX + 1.0 - POSX) * DELTADISTX
270 IF RAYDIRY<0.0 THEN STEPY=-1; SIDEDISTY = (POSY - MAPY) * DELTADISTY ELSE STEPY = 1; SIDEDISTY = (MAPY + 1.0 - POSY) * DELTADISTY
280 WHILE HIT = 0
290 IF SIDEDISTX < SIDEDISTY THEN SIDEDISTX = SIDEDISTX + DELTADISTX; MAPX = MAPX + STEPX; SIDE=0 ELSE SIDEDISTY = SIDEDISTY + DELTADISTY; MAPY = MAPY + STEPY; SIDE=1
300 IF WORLDMAP(MAPY, MAPX) > 0 THEN HIT=1
310 WEND
320 IF SIDE = 0 THEN PERPWALLDIST = (MAPX - POSX + (1.0 - STEPX) / 2.0) / RAYDIRX ELSE PERPWALLDIST = (MAPY - POSY + (1.0 - STEPY) / 2.0) / RAYDIRY
330 LINEHEIGHT = CINT(SCREENHEIGHT / PERPWALLDIST)
340 DRAWSTART = -LINEHEIGHT \ 2 + SCREENHEIGHT \ 2
350 IF DRAWSTART < 0 THEN DRAWSTART = 0
360 DRAWEND = LINEHEIGHT \ 2 + SCREENHEIGHT \ 2
370 IF DRAWEND >= SCREENHEIGHT THEN DRAWEND = SCREENHEIGHT - 1
380 TILE = WORLDMAP(MAPY, MAPX); COLOUR = 255
390 IF TILE = 1 THEN COLOUR = 224
400 IF TILE = 2 THEN COLOUR = 28
410 IF TILE = 3 THEN COLOUR = 3
420 IF TILE = 0 THEN COLOUR = 0
430 IF SIDE = 1 THEN COLOUR = COLOUR \ 2
450 LINE(X,DRAWSTART)-(X,DRAWEND),COLOUR
460 REM LINE(X+1,DRAWSTART)-(X+1,DRAWEND),COLOUR
470 NEXT
480 KEYPRESS=INKEY()
490 IF KEYPRESS = 119 THEN GOSUB 800
500 IF KEYPRESS = 115 THEN GOSUB 830
510 IF KEYPRESS = 97 THEN GOSUB 860
520 IF KEYPRESS = 100 THEN GOSUB 930
590 WAIT
700 WEND
800 IF WORLDMAP(CINT(POSY), CINT(POSX + DIRX * MOVESPEED)) = 0 THEN POSX = POSX + DIRX * MOVESPEED
810 IF WORLDMAP(CINT(POSY + DIRY * MOVESPEED), CINT(POSX)) = 0 THEN POSY = POSY + DIRY * MOVESPEED
820 RETURN
830 IF WORLDMAP(CINT(POSY), CINT(POSX - DIRX * MOVESPEED)) = 0 THEN POSX = POSX - DIRX * MOVESPEED
840 IF WORLDMAP(CINT(POSY - DIRY * MOVESPEED), CINT(POSX)) = 0 THEN POSY = POSY - DIRY * MOVESPEED
850 RETURN
860 OLDDIRX = DIRX
870 DIRX = DIRX * COS(ROTSPEED) - DIRY * SIN(ROTSPEED)
880 DIRY = OLDDIRX * SIN(ROTSPEED) + DIRY * COS(ROTSPEED)
890 OLDPLANEX = PLANEX
900 PLANEX = PLANEX * COS(ROTSPEED) - PLANEY * SIN(ROTSPEED)
910 PLANEY = OLDPLANEX * SIN(ROTSPEED) + PLANEY * COS(ROTSPEED)
920 RETURN
930 OLDDIRX = DIRX
940 DIRX = DIRX * COS(-ROTSPEED) - DIRY * SIN(-ROTSPEED)
950 DIRY = OLDDIRX * SIN(-ROTSPEED) + DIRY * COS(-ROTSPEED)
960 OLDPLANEX = PLANEX
970 PLANEX = PLANEX * COS(-ROTSPEED) - PLANEY * SIN(-ROTSPEED)
980 PLANEY = OLDPLANEX * SIN(-ROTSPEED) + PLANEY * COS(-ROTSPEED)
990 RETURN
1000 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
1010 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1020 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1030 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1040 DATA 1,0,0,0,0,0,2,2,2,2,2,0,0,0,0,3,0,3,0,3,0,0,0,1
1050 DATA 1,0,0,0,0,0,2,0,0,0,2,0,0,0,0,0,0,0,0,0,0,0,0,1
1060 DATA 1,0,0,0,0,0,2,0,0,0,2,0,0,0,0,3,0,0,0,3,0,0,0,1
1070 DATA 1,0,0,0,0,0,2,0,0,0,2,0,0,0,0,0,0,0,0,0,0,0,0,1
1080 DATA 1,0,0,0,0,0,2,2,0,2,2,0,0,0,0,3,0,3,0,3,0,0,0,1
1090 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1100 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1110 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1120 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1130 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1140 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1150 DATA 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1160 DATA 1,4,4,4,4,4,4,4,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1170 DATA 1,4,0,4,0,0,0,0,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1180 DATA 1,4,0,0,0,0,5,0,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1190 DATA 1,4,0,4,0,0,0,0,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1200 DATA 1,4,0,4,4,4,4,4,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1210 DATA 1,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1220 DATA 1,4,4,4,4,4,4,4,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
1230 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
